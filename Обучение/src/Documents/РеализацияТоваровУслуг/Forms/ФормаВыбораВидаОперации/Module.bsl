
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
	Параметры.Свойство("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Параметры.Свойство("Основание", Основание);
	Параметры.Свойство("Ключ", Ключ);
	
	Для каждого ЗначениеПеречисления Из Перечисления.ВидыОперацийРеализацияТоваров Цикл
		СписокВидовОпераций.Добавить(ЗначениеПеречисления);
	КонецЦикла;
	
	Если Параметры.Свойство("ВидОперации") Тогда
	    ЭлементСписка = СписокВидовОпераций.НайтиПоЗначению(Параметры.ВидОперации);
		Если ЭлементСписка <> Неопределено Тогда
			Элементы.СписокВидовОпераций.ТекущаяСтрока = ЭлементСписка.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.СписокВидовОпераций.ТекущиеДанные;
	ВидОперации = ТекущиеДанные.Значение;
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(Ключ) Тогда
		ПараметрыФормы.Вставить("Ключ", Ключ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияЗаполнения) Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		ПараметрыФормы.Вставить("Основание", Основание);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", ЗначениеКопирования);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ИзменитьВидОперации", ВидОперации);
	Закрыть();
	
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма." + ПолучитьИмяФормыПоВидуОперации(ВидОперации), ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяФормыПоВидуОперации(ВидОперации)

	Соотвествие = Документы.РеализацияТоваровУслуг.ПолучитьСоотвествиеФормИВидовОпераций();
	Возврат Соотвествие[ВидОперации];	

КонецФункции // ПолучитьИмяФормыПоВидуОперации()

&НаКлиенте
Процедура СписокВидовОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Выбрать("");	
	
КонецПроцедуры














