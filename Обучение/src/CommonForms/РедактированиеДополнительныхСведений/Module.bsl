
&НаКлиенте
Процедура ДополнительныеСведенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСведенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Ссылка", ССылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПредопределенныйЭлемент = Справочники.НаборыДополнительныхРеквизитовИСведений.ПолучитьСсылкуПредопределенного(ССылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство КАК Свойство,
		|	ДополнительныеСведения.Значение
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство = ДополнительныеСведения.Свойство
		|			И (ДополнительныеСведения.Объект = &Ссылка)
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка = &ПредопределенныйЭлемент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Свойство
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ПредопределенныйЭлемент", ПредопределенныйЭлемент);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДополнительныеСведения.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСведенияЗначениеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()

	Набор = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(ССылка);
	
	Для каждого Стр Из ДополнительныеСведения Цикл
	
		Если ЗначениеЗаполнено(Стр.Значение) Тогда
			НовСтр = Набор.Добавить();
			НовСтр.Объект = ССылка;
			ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		КонецЕсли;
	
	КонецЦикла;
	
	Набор.Записать();

КонецПроцедуры // ЗаписатьНаСервере()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
					   "Данные были изменены. Сохранить изменения?",
					   РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда	
		ЗаписатьНаСервере();
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры















