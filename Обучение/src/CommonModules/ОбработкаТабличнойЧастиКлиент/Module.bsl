
Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Если НеобходимВызовСервера(ТекущаяСтрока, СтруктураДействий) Тогда
		ТекущаяСтрокаСтруктура = ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока,СтруктураДействий);
		ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(ТекущаяСтрокаСтруктура, СтруктураДействий);		
	    ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ТекущаяСтрокаСтруктура);
		Возврат;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьЦенуПоСуммеВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);

КонецПроцедуры

Функция ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока, СтруктураДействий)
	
	СтруктураПолейТЧ = Новый Структура;
	
	Если СтруктураДействий.Свойство("ЗаполнитьЦенуПродажи") Тогда
	    СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("Цена", 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму") или СтруктураДействий.Свойство("ПересчитатьЦенуПоСумме") Тогда
		СтруктураПолейТЧ.Вставить("Количество", 0);
		СтруктураПолейТЧ.Вставить("Цена", 0);
		СтруктураПолейТЧ.Вставить("Сумма", 0);
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(СтруктураПолейТЧ, ТекущаяСтрока);
	
	Возврат СтруктураПолейТЧ;
	
КонецФункции // ()

Функция НеобходимВызовСервера(ТекущаяСтрока, СтруктураДействий)

	Если СтруктураДействий.Свойство("ЗаполнитьЦенуПродажи") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции // НеобходимВызовСервера()
