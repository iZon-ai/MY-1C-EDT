Функция ПолучитьМассивРасширенийГрафическихФайлов() Экспорт

	Фильтр = ПолучитьФильтрГрафическихФайлов();
	Фильтр = СтрЗаменить(Фильтр, "*.", ".");
	мРасширения = СтрРазделить(Фильтр, ";", Ложь);
	
	Возврат мРасширения;

КонецФункции // ПолучитьМассивРасширенийГрафическихФайлов()

Функция ПолучитьФильтрГрафическихФайлов() Экспорт

	Возврат "*.jpg;*.jpeg;*.png;*.bmp;*.ico;*.gif;*.tif;*.emf;*.wmf;";	

КонецФункции // ПолучитьФильтрГрафическихФайлов()

Процедура ЗагрузитьФайл(Параметры) Экспорт

	ОповещениеПослеУстановки = Новый ОписаниеОповещения("ПослеПопыткиПодключенияИУстановкиРасширения", ЭтотОбъект, Параметры);
	ПодключениеРасширенияРаботыСФайлами(ОповещениеПослеУстановки);

КонецПроцедуры

Процедура ПослеПопыткиПодключенияИУстановкиРасширения(Результат, ДополнительныеПараметры) Экспорт

	Если Результат Тогда
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.Фильтр = ДополнительныеПараметры.Фильтр;
		Диалог.Показать(Новый ОписаниеОповещения("ДиалогПоказатьЗавершение", ЭтотОбъект, ДополнительныеПараметры));
		
	Иначе
		
		НачатьПомещениеФайла(Новый ОписаниеОповещения("НачатьПомещениеФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры)
							  ,,, Истина, ДополнительныеПараметры.УникальныйИдентификатор);
	
	КонецЕсли;	

КонецПроцедуры

Процедура НачатьПомещениеФайлаЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Результат);
	СтруктураРезультата.Вставить("Адрес", Адрес);
	СтруктураРезультата.Вставить("ВыбранноеИмяФайла", ВыбранноеИмяФайла);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияЗавершения, СтруктураРезультата);

КонецПроцедуры

Процедура ДиалогПоказатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		СтруктураРезультата = Новый Структура;
		СтруктураРезультата.Вставить("Результат", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияЗавершения, СтруктураРезультата);
		
	Иначе
		
		Файл = Новый Файл(Результат[0]);
		ДополнительныеПараметры.Вставить("ИмяФайла", Результат[0]);
		Файл.НачатьПолучениеРазмера(Новый ОписаниеОповещения("ПолучениеРазмераЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
	КонецЕсли;	

КонецПроцедуры

Процедура ПолучениеРазмераЗавершение(Размер, ДополнительныеПараметры) Экспорт
	
	Если Размер>20000000 Тогда
		
		ПоказатьПредупреждение(,"Размер файла превышает максимально допустимый размер",,"Внимание");
		СтруктураРезультата = Новый Структура;
		СтруктураРезультата.Вставить("Результат", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияЗавершения, СтруктураРезультата);
		
	Иначе	
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ДополнительныеПараметры.ИмяФайла);
		ПомещаемыеФайлы.Добавить(Описание);
		
		НачатьПомещениеФайлов(Новый ОписаниеОповещения("ПомещениеФайловЗавершение", ЭтотОбъект, ДополнительныеПараметры),
							  ПомещаемыеФайлы, , Ложь, ДополнительныеПараметры.УникальныйИдентификатор);
	
	КонецЕсли;	

КонецПроцедуры

Процедура ПомещениеФайловЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = Новый Структура;
	Если ПомещенныеФайлы = Неопределено Тогда
		СтруктураРезультата.Вставить("Результат", Ложь);
	Иначе
		СтруктураРезультата.Вставить("Результат", Истина);
		СтруктураРезультата.Вставить("Адрес",  ПомещенныеФайлы[0].Хранение);
		СтруктураРезультата.Вставить("ВыбранноеИмяФайла", ПомещенныеФайлы[0].Имя);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияЗавершения, СтруктураРезультата);

КонецПроцедуры

// Установка
Процедура ПодключениеРасширенияРаботыСФайлами(ОповещениеПослеУстановки)

	#Если Не ВебКлиент Тогда
		ВыполнитьОбработкуОповещения(ОповещениеПослеУстановки, Истина);
		Возврат;
	#КонецЕсли
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановки", ОповещениеПослеУстановки);
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПодключениеРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры));

КонецПроцедуры

Процедура ПодключениеРасширенияРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановки, Истина);
		Возврат;		
	КонецЕсли;	
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86
		или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86_64
		или ПрисоединенныеФайлыКлиентПовторно.НеОтображатьДиалогУстановкиРасширения() Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановки, Ложь);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.УстановкаРасширенияРаботыСФайлами"
	             ,,,,,, Новый ОписаниеОповещения("ПодключениеРасширенияРаботыСФайламиЗавершениеЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

Процедура ПодключениеРасширенияРаботыСФайламиЗавершениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановки, Результат="ОК");	
	
КонецПроцедуры















