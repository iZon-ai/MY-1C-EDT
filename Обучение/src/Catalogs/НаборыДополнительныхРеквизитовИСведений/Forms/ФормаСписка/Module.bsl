
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоДопСведения = Ложь;
	Список.Параметры.УстановитьЗначениеПараметра("ЭтоДопСведения", ЭтоДопСведения);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьСвойства();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоДопСведенияПриИзменении(Элемент)
	
	ЗаполнитьСвойства();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСвойства()

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТаблицаСвойств.Очистить();
	Иначе
		ЗаполнитьСвойстваНаСервере(ТекущиеДанные.Ссылка);	
	КонецЕсли;

КонецПроцедуры // ЗаполнитьСвойства()

&НаСервере
Процедура ЗаполнитьСвойстваНаСервере(Ссылка)

	Список.Параметры.УстановитьЗначениеПараметра("ЭтоДопСведения", ЭтоДопСведения);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Свойство
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Свойство
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если ЭтоДопСведения Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".ДополнительныеРеквизиты", ".ДополнительныеСведения");		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаСвойств.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры // ЗаполнитьСвойстваНаСервере()

&НаКлиенте
Процедура ТаблицаСвойствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаСвойств.ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Элементы.ТаблицаСвойств.ТекущиеДанные.Свойство);
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("ЭтоДополнительноеСведение", ЭтоДопСведения);
	ЗначенияЗаполнения.Вставить("НаборСвойств", ТекущиеДанные.Ссылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения" Тогда
		Элементы.Список.Обновить();
		ЗаполнитьСвойства();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбщее(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("ЭтоДополнительноеСведение", ЭтоДопСведения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбораРеквизитовИСведений", "ВыборОбщего");
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоОбразцу(Команда)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("ЭтоДополнительноеСведение", ЭтоДопСведения);
	Отбор.Вставить("ДополнительныеЗначенияИспользуются", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбораРеквизитовИСведений", "ВыборПоОбразцу");
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") и ВыбранноеЗначение.Свойство("РежимВыбораРеквизитовИСведений") Тогда
	
		Если ВыбранноеЗначение.РежимВыбораРеквизитовИСведений = "ВыборОбщего" Тогда
			ДобавитьНаСервере(Элементы.Список.ТекущиеДанные.Ссылка, ВыбранноеЗначение.Значение);
		ИначеЕсли ВыбранноеЗначение.РежимВыбораРеквизитовИСведений = "ВыборПоОбразцу" Тогда
			ЗначенияЗаполнения = Новый Структура();
			ЗначенияЗаполнения.Вставить("ЭтоДополнительноеСведение", ЭтоДопСведения);
			ЗначенияЗаполнения.Вставить("НаборСвойств", Элементы.Список.ТекущиеДанные.Ссылка);
			ЗначенияЗаполнения.Вставить("ВладелецДополнительныхЗначений", ВыбранноеЗначение.Значение);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНаСервере(Набор, Свойство)

	НаборОбъект = Набор.ПолучитьОбъект();
	Если ЭтоДопСведения Тогда
		ТабЧасть = НаборОбъект.ДополнительныеСведения;
	Иначе
		ТабЧасть = НаборОбъект.ДополнительныеРеквизиты;
	КонецЕсли;
	
	Если ТабЧасть.Найти(Свойство, "Свойство") = Неопределено Тогда
		НовСтр = ТабЧасть.Добавить();
		НовСтр.Свойство = Свойство;
		НаборОбъект.Записать();
		
		Элементы.Список.Обновить();
		ЗаполнитьСвойстваНаСервере(Набор);
	КонецЕсли;

КонецПроцедуры // ДобавитьНаСервере()















