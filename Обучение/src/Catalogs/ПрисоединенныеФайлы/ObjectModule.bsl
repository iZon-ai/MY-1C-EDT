
Процедура ПриЗаписи(Отказ)
	
	Если ТипХранения = Перечисления.ТипыХраненияФайлов.ВБазе Тогда
		СохранитьВБазе(Отказ);	
	Иначе	
	    СохранитьВКаталоге(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьВКаталоге(Отказ)

	ДвоичныеДанные = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("АдресФайла") Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДополнительныеСвойства.АдресФайла);	
	КонецЕсли;

	Если ДополнительныеСвойства.ИзменилсяТипХранения Тогда
		ЗаписьРегистра = РегистрыСведений.ПрисоединенныеФайлы.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.ПрисоединенныйФайл = Ссылка;
		
		Если ДвоичныеДанные = Неопределено Тогда
			ЗаписьРегистра.Прочитать();
			ДвоичныеДанные = ЗаписьРегистра.Хранилище.Получить();
		КонецЕсли;
		
		ЗаписьРегистра.Удалить();		
	КонецЕсли;
	
	Если ДвоичныеДанные<>Неопределено Тогда
		ПутьКФайлу = ПрисоединенныеФайлыСервер.ПолучитьПутьКФайлуНаСервере(ЭтотОбъект);
		Попытка
			ДвоичныеДанные.Записать(ПутьКФайлу);
		Исключение
			Отказ = Истина;
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьВБазе(Отказ)

	ДвоичныеДанные = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("АдресФайла") Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДополнительныеСвойства.АдресФайла);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ИзменилсяТипХранения Тогда
		ПутьКФайлу = ПрисоединенныеФайлыСервер.ПолучитьПутьКФайлуНаСервере(ЭтотОбъект);
		Файл = Новый Файл(ПутьКФайлу);
		Если НЕ Файл.Существует() Тогда
			Отказ = Истина;
			Сообщить("Файл недоступен");
			Возврат;
		КонецЕсли;
		
		Если ДвоичныеДанные = Неопределено Тогда
			ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКФайлу);
		КонецЕсли;
		
		УдалитьФайлы(ПутьКФайлу);
	КонецЕсли;
	
	Если ДвоичныеДанные<>Неопределено Тогда
		ЗаписьРегистра = РегистрыСведений.ПрисоединенныеФайлы.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.ПрисоединенныйФайл = Ссылка;
		ЗаписьРегистра.Хранилище = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		ЗаписьРегистра.Записать();	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Расширения = Новый Соответствие;
	Расширения.Вставить(".doc", 2);
	Расширения.Вставить(".docx", 2);
	Расширения.Вставить(".xls", 4);
	Расширения.Вставить(".xlsx", 4);
	Расширения.Вставить(".pdf", 6);
	Расширения.Вставить(".jpg", 8);
	
	ИндексКартинки = Расширения[Расширение];
	
	Если ПометкаУдаления Тогда
		ИндексКартинки = ИндексКартинки + 1;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ИзменилсяТипХранения",Не ЭтоНовый() и ТипХранения<>Ссылка.ТипХранения);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипХранения = Константы.ТипХраненияФайлов.Получить();
	Если Не ЗначениеЗаполнено(ТипХранения) Тогда
		ТипХранения = Перечисления.ТипыХраненияФайлов.ВБазе;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ТипХранения = Перечисления.ТипыХраненияФайлов.НаСервере Тогда
		ПутьКФайлу = ПрисоединенныеФайлыСервер.ПолучитьПутьКФайлуНаСервере(ЭтотОбъект);
		УдалитьФайлы(ПутьКФайлу);
	КонецЕсли;	
	
КонецПроцедуры



