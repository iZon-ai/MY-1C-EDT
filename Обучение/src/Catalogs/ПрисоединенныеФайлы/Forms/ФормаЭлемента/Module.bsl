
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) и Параметры.Свойство("Адрес") Тогда
		ЗаполнитьНаСервере(АдресИзображения, АдресФайла, Объект.Наименование, Объект.Расширение, Параметры.Адрес, Параметры.ВыбранноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьНаСервере(АдресИзображения, АдресФайла, Наименование, Расширение, Адрес, ВыбранноеИмяФайла)

	Если ЭтоАдресВременногоХранилища(АдресИзображения) Тогда
		УдалитьИзВременногоХранилища(АдресИзображения);
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		УдалитьИзВременногоХранилища(АдресФайла);
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранноеИмяФайла);
	Наименование = Файл.ИмяБезРасширения;
	Расширение = НРег(Файл.Расширение);
	
	Если ПрисоединенныеФайлыСервер.ЭтоГрафическийФайл(Расширение) Тогда
		АдресИзображения = Адрес;
		АдресФайла = "";
	Иначе
		АдресФайла = Адрес;
		АдресИзображения = "";
	КонецЕсли;

КонецПроцедуры // ЗаполнитьНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) и Объект.Наименование<>"" Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность и ЭтоАдресВременногоХранилища(АдресИзображения) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("АдресФайла", АдресИзображения);
	ИначеЕсли Модифицированность и ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("АдресФайла", АдресФайла);	
	КонецЕсли;		
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресИзображения = ПрисоединенныеФайлыВызовСервера.ПолучитьНавигационнуюСсылкуФайла(Объект.Ссылка, Объект.Расширение, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДругойФайл(Команда)
	
	Фильтр = ПрисоединенныеФайлыКлиент.ПолучитьФильтрГрафическихФайлов();
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыЗагрузки.Вставить("Фильтр","Все файлы (*.*)|*.*|Файлы изображений ("+Фильтр+")|"+Фильтр);
	ПараметрыЗагрузки.Вставить("ОписаниеОповещенияЗавершения", Новый ОписаниеОповещения("ВыбратьДругойФайлЗавершение", ЭтотОбъект));
	
	ПрисоединенныеФайлыКлиент.ЗагрузитьФайл(ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДругойФайлЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Результат Тогда
		ЗаполнитьНаСервере(АдресИзображения, АдресФайла, Объект.Наименование, Объект.Расширение, Результат.Адрес, Результат.ВыбранноеИмяФайла);
		Модифицированность = Истина;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	Если ЗначениеЗаполнено(АдресИзображения) Тогда
		АдресФайлаДляСохранения = АдресИзображения;
	ИначеЕсли ЗначениеЗаполнено(АдресФайла) Тогда	
		АдресФайлаДляСохранения = АдресФайла;
	Иначе
		АдресФайлаДляСохранения = ПрисоединенныеФайлыВызовСервера.ПолучитьНавигационнуюСсылкуФайла(Объект.Ссылка, Объект.Расширение, УникальныйИдентификатор, Истина);
	КонецЕсли;
	
	ПолучитьФайл(АдресФайлаДляСохранения, СокрЛП(Объект.Наименование)+Объект.Расширение, Истина);
	
КонецПроцедуры













