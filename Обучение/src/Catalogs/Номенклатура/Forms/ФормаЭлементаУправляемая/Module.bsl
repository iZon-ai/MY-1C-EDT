
&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьИзображениеЗавершение", ЭтотОбъект),
					   "Для выбора изображения необходимо записать объект. Записать?",
					   РежимДиалогаВопрос.ДаНет);
		Возврат;				   
					   
	КонецЕсли;
	
	ЗагрузитьИзображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзображение()

	Фильтр = ПрисоединенныеФайлыКлиент.ПолучитьФильтрГрафическихФайлов();
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыЗагрузки.Вставить("Фильтр", "Файлы изображений ("+Фильтр+")|"+Фильтр);
	ПараметрыЗагрузки.Вставить("ОписаниеОповещенияЗавершения", Новый ОписаниеОповещения("ЗагрузкаФайлаЗавершение", ЭтотОбъект));
	
	ПрисоединенныеФайлыКлиент.ЗагрузитьФайл(ПараметрыЗагрузки);

КонецПроцедуры // ЗагрузитьИзображение()

&НаКлиенте
Процедура ЗагрузкаФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Результат Тогда
		Объект.ИзображениеТовара = СоздатьЭлементаСправочникаНаСервере(Результат.Адрес, Результат.ВыбранноеИмяФайла, Объект.Ссылка);
		АдресИзображения = Результат.Адрес;
		Модифицированность = Истина;
		ОповеститьОбИзменении(Объект.ИзображениеТовара);
	КонецЕсли;

КонецПроцедуры // ЗагрузкаФайлаЗавершение()

&НаСервереБезКонтекста
Функция СоздатьЭлементаСправочникаНаСервере(Адрес, ВыбранноеИмяФайла, Ссылка)

	Файл = Новый Файл(ВыбранноеИмяФайла);
	СправочникОбъект = Справочники.ПрисоединенныеФайлы.СоздатьЭлемент();
	СправочникОбъект.Наименование = Файл.ИмяБезРасширения;
	СправочникОбъект.Расширение = Файл.Расширение;
	СправочникОбъект.ВладелецФайла = Ссылка;
	СправочникОбъект.ДополнительныеСвойства.Вставить("АдресФайла", Адрес);
	СправочникОбъект.Заполнить(Неопределено);
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;	

КонецФункции // СоздатьЭлементаСправочникаНаСервере()

&НаКлиенте
Процедура АдресИзображенияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьИзображение("");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ИзображениеТовара) Тогда
	
		АдресИзображения = ПрисоединенныеФайлыВызовСервера.ПолучитьНавигационнуюСсылкуФайла(Объект.ИзображениеТовара, "", УникальныйИдентификатор, Истина);	
	
	КонецЕсли;
	
	СоздатьДополнительныеРеквизиты();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзображение(Команда)
	
	Если ЗначениеЗаполнено(Объект.ИзображениеТовара) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Объект.ИзображениеТовара);
		ОткрытьФорму("Справочник.ПрисоединенныеФайлы.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображение(Команда)
	
	Если ЗначениеЗаполнено(Объект.ИзображениеТовара) Тогда
	
		Если СтрНайти(АдресИзображения,"tempstorage")>0 Тогда
			УдалитьИзВременногоХранилища(АдресИзображения);
		КонецЕсли;	
		
		Объект.ИзображениеТовара = "";
		АдресИзображения = "";
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзПрисоединенныхФайлов(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Отбор = Новый Структура();
		Отбор.Вставить("ВладелецФайла", Объект.Ссылка);
		Отбор.Вставить("Расширение", ПрисоединенныеФайлыКлиент.ПолучитьМассивРасширенийГрафическихФайлов());
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Справочник.ПрисоединенныеФайлы.ФормаСписка", ПараметрыФормы, ЭтаФорма ,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ПрисоединенныеФайлы") Тогда
		
		Объект.ИзображениеТовара = ВыбранноеЗначение;
		Если СтрНайти(АдресИзображения,"tempstorage")>0 Тогда
			УдалитьИзВременногоХранилища(АдресИзображения);	
		КонецЕсли;
		
		АдресИзображения = ПрисоединенныеФайлыВызовСервера.ПолучитьНавигационнуюСсылкуФайла(ВыбранноеЗначение, "", УникальныйИдентификатор, Истина);
		Модифицированность = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДополнительныеРеквизиты()

	ПредопределенныйЭлемент = Справочники.НаборыДополнительныхРеквизитовИСведений.ПолучитьСсылкуПредопределенного(Объект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Свойство,
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Наименование,
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.ТипЗначения КАК ТипЗначения,
		|	НоменклатураДополнительныеРеквизиты.Значение
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
		|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = НоменклатураДополнительныеРеквизиты.Свойство
		|			И (НоменклатураДополнительныеРеквизиты.Ссылка = &Ссылка)
		|ГДЕ
		|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &ПредопределенныйЭлемент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Свойство
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ПредопределенныйЭлемент", ПредопределенныйЭлемент);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	мРеквизиты = Новый Массив;
	НомерРеквизита = 0;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НомерРеквизита = НомерРеквизита + 1;
		Имя = "ДополнительныйРеквизит_"+Формат(НомерРеквизита,"ЧГ=");
		Реквизит = Новый РеквизитФормы(Имя, ВыборкаДетальныеЗаписи.ТипЗначения, , ВыборкаДетальныеЗаписи.СвойствоНаименование, Истина);
		мРеквизиты.Добавить(Реквизит);
		
	КонецЦикла;
	
	ИзменитьРеквизиты(мРеквизиты);
	ВыборкаДетальныеЗаписи.Сбросить();
	НомерРеквизита = 0;
	мСвойства = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	
		НомерРеквизита = НомерРеквизита + 1;
		Имя = "ДополнительныйРеквизит_"+Формат(НомерРеквизита,"ЧГ=");
		ЭтаФорма[Имя] = ВыборкаДетальныеЗаписи.Значение;
		
		Поле = Элементы.Добавить(Имя, Тип("ПолеФормы"), Элементы.ГруппаДополнительныеРеквизиты);
		Если ВыборкаДетальныеЗаписи.ТипЗначения.СодержитТип(Тип("Булево")) Тогда
			Поле.Вид = ВидПоляФормы.ПолеФлажка;
		Иначе
			Поле.Вид = ВидПоляФормы.ПолеВвода;
		КонецЕсли;
		
		Поле.ПутьКДанным = Имя;
		Поле.Заголовок = ВыборкаДетальныеЗаписи.СвойствоНаименование;
		
		Если ВыборкаДетальныеЗаписи.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ДополнительныеЗначения")) Тогда
			ПараметрыВыбораЗначения = Новый Массив;
			ПараметрыВыбораЗначения.Добавить(Новый ПараметрВыбора("Отбор.Владелец", ВыборкаДетальныеЗаписи.Свойство));
			Поле.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЗначения);			
		КонецЕсли;
		мСвойства.Добавить(ВыборкаДетальныеЗаписи.Свойство);
		
	КонецЦикла;
	
	АдресМассиваСвойств = ПоместитьВоВременноеХранилище(мСвойства, УникальныйИдентификатор);

КонецПроцедуры // СоздатьДополнительныеРеквизиты()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	мСвойства = ПолучитьИзВременногоХранилища(АдресМассиваСвойств);
	ТекущийОбъект.ДополнительныеРеквизиты.Очистить();
	
	Для А = 1 По мСвойства.Количество() Цикл
	
		Имя = "ДополнительныйРеквизит_"+Формат(А,"ЧГ=");
		Если ЗначениеЗаполнено(ЭтаФорма[Имя]) Тогда
			НовСтр = ТекущийОбъект.ДополнительныеРеквизиты.Добавить();
			НовСтр.Свойство = мСвойства[А-1];
			НовСтр.Значение = ЭтаФорма[Имя];		
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры














