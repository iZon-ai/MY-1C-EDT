
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	ЗапрашиватьКоличество = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если ЗапрашиватьКоличество Тогда
		Количество = 1;
		ПоказатьВводЧисла(Новый ОписаниеОповещения("СписокВыборЗавершение", ЭтотОбъект),
						  Количество,
						  "Введите количество:",
						  10,3);
	Иначе
		ВыбратьНоменклатуру(1);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗавершение(Число, ДополнительныеПараметры) Экспорт
	
	Если Число<>Неопределено и Число<>0 Тогда
		ВыбратьНоменклатуру(Число);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНоменклатуру(Количество)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	СтруктураВыбора = Новый Структура;
	СтруктураВыбора.Вставить("Номенклатура", ТекущиеДанные.Ссылка);
	СтруктураВыбора.Вставить("ВидНоменклатуры", ТекущиеДанные.ВидНоменклатуры);
	СтруктураВыбора.Вставить("Количество", Количество);
	
	ОповеститьОВыборе(СтруктураВыбора);

КонецПроцедуры // ВыбратьНоменклатуру()











