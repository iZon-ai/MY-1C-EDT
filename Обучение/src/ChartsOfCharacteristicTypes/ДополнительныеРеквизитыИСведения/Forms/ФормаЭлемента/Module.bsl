
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовый и ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
		
		СправочникОбъект = Объект.НаборСвойств.ПолучитьОбъект();
		Если Объект.ЭтоДополнительноеСведение Тогда
			НовСтр = СправочникОбъект.ДополнительныеСведения.Добавить();	
		Иначе	
		    НовСтр = СправочникОбъект.ДополнительныеРеквизиты.Добавить();
		КонецЕсли;
		
		НовСтр.Свойство = Объект.Ссылка;
		СправочникОбъект.Записать();
	
	КонецЕсли;
	
	НастроитьЗаголовок();
	УстановитьОтборПоВладельцу();

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Не ЗначениеЗаполнено(Объект.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДополнительныеРеквизитыИСведения");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьЗаголовок();
	УстановитьОтборПоВладельцу();
	Элементы.ДополнительныеЗначения.Видимость = Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ДополнительныеЗначения"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВладельцу()

	ЭлементыОтбора = ДополнительныеЗначения.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ЭлементОтбора.Представление = "ОтборПоВладельцу" Тогда
			ЭлементОтбора.ПравоеЗначение = ?(ЗначениеЗаполнено(Объект.ВладелецДополнительныхЗначений), Объект.ВладелецДополнительныхЗначений, Объект.Ссылка);
			Прервать;	
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // УстановитьОтборПоВладельцу()

&НаСервере
Процедура НастроитьЗаголовок()

	Заголовок = ?(Объект.ЭтоДополнительноеСведение, "Дополнительное сведение", "Дополнительный реквизит");
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		 Заголовок = Заголовок + " (создание)";
	КонецЕсли;

КонецПроцедуры // НастроитьЗаголовок()

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	
	Элементы.ДополнительныеЗначения.Видимость = Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ДополнительныеЗначения"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ДополнительныеЗначенияПередНачаломДобавленияЗавершение", ЭтотОбъект),
					   "Добавление возможно только после записи. Записать?",
					    РежимДиалогаВопрос.ДаНет);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЗначенияПередНачаломДобавленияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();	
		Элементы.ДополнительныеЗначения.ДобавитьСтроку();
	КонецЕсли;	
	
КонецПроцедуры












